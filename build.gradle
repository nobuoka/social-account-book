buildscript {
    ext.kotlin_version = '1.2.71'
    ext.ktor_version = '0.9.5'
    ext.kotlinx_coroutines_version = '0.30.2'
    ext.kotlinx_html_version = '0.6.11'
    ext.junit_version = '5.2.0'
    ext.slf4j_version = '1.7.25'
    ext.okhttp_version = '3.11.0'
    ext.mockito_version = '2.19.1'
    ext.mockk_version = '1.8.5'
    ext.h2db_version = '1.4.197'
    ext.jacoco_version = "0.8.2"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/ktor" }
        maven { url 'https://oss.jfrog.org/artifactory/list/oss-snapshot-local' }
    }
}

repositories {
    jcenter()
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = jacoco_version
}

task jacocoMerge(type: JacocoMerge) {
    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            executionData p.tasks.test.jacoco.destinationFile
            dependsOn(p.tasks.test)
        }
    }
}

task jacocoMergedReport(type: JacocoReport, dependsOn: [tasks.jacocoMerge]) {
    executionData jacocoMerge.destinationFile
    sourceDirectories = files()
    classDirectories = files()
    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('java')) {
            sourceDirectories = sourceDirectories + files([p.sourceSets.main.allJava.srcDirs])
            classDirectories = classDirectories + p.sourceSets.main.output
        }
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

task check(dependsOn: [tasks.jacocoMergedReport])
task build(dependsOn: [tasks.check])

task clean(type: Delete) {
    delete rootProject.buildDir
}
